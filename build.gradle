buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.1'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'

version = '1.0.0'
group = 'com.aeonlucid.discordsync'
archivesBaseName = 'discordsync'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: 'official', version: '1.16.5'
    runs {
        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                discordsync {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

jar {
    manifest {
        attributes([
            "Maven-Artifact" : "${project.group}:${project.archivesBaseName}:${project.version}",
            "Timestamp" : System.currentTimeMillis(),
            "Specification-Title": "discordsync",
            "Specification-Vendor": "AeonLucid",
            "Specification-Version": "1",
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"AeonLucid",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}

// Shadow
configurations {
    embed
    compileOnly.extendsFrom(embed)
}

shadowJar {
    configurations = [project.configurations.embed]

    // https://imperceptiblethoughts.com/shadow/configuration/relocation/#filtering-relocation
    def packageShadow = 'com.aeonlucid.discordsync.shadow'
    def packages = [
            'club.minnced',
            'com.fasterxml',
            'com.google.gson',
            'gnu.trove',
            'javax.annotation',
            'net.dv8tion',
            'okhttp3',
            'okio',
            'org.apache',
            'org.intellij',
            'org.jetbrains',
            'org.jetbrains',
            'org.json',
            'org.json',
            'org.slf4j',
            'com.iwebpp',
            'com.iwebpp',
            'com.neovisionaries',
    ]

    for (p in packages) {
        relocate p, packageShadow + "." + p
    }

    // https://imperceptiblethoughts.com/shadow/configuration/minimizing/
    minimize()
}

reobf {
    shadowJar {
        dependsOn tasks.createMcpToSrg
        mappings = tasks.createMcpToSrg.outputs.files.singleFile
    }
}

artifacts {
    archives tasks.shadowJar
}

tasks.build.dependsOn(tasks.shadowJar)

// Dependencies
dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.22'

    embed('net.dv8tion:JDA:5.0.0-alpha.2') {
        exclude module: 'opus-java'
    }

    embed 'club.minnced:discord-webhooks:0.7.4'
}